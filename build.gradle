plugins {
    id 'java'
    id "java-library"
    id "maven-publish"
    id "signing"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'cn.alapi'
version '1.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.googlecode.json-simple:json-simple:1.1.1"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching "*Test"
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

javadoc {
    description = "Java SDK for ALAPI"
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.alapi'
            artifactId = 'javasdk'
            version = '1.0.2'
            from components.java

            artifact sourceJar
            artifact javadocJar

            pom {
                name = 'alapi-javasdk'
                description = 'Java SDK for ALAPI.'
                url = 'https://github.com/ALAPI-SDK/JAVA-SDK'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }
                developers {
                    developer {
                        name = 'yeliulee'
                        email = 'yeliuleet@gmail.com'
                        roles = ['Java Developer']
                        url = "https://www.yeliulee.com"
                    }
                }
                scm {
                    url = 'https://github.com/ALAPI-SDK/JAVA-SDK'
                    connection = 'scm:git:https://github.com/ALAPI-SDK/JAVA-SDK.git'
                    developerConnection = 'scm:git:git@github.com:ALAPI-SDK/JAVA-SDK.git'
                }
            }
        }
    }

    repositories {
        maven {
            name "oss"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

jar.enabled = false
shadowJar {
    archiveClassifier.set(null)
    archiveFileName.set("alapi-javasdk-${archiveVersion.get()}.jar")
    configurations = [project.configurations.compileClasspath]
}

assemble.dependsOn(shadowJar)